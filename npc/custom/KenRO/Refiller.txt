//-- Refill and Healer
turbo_room,103,115,6	script	Refill and Heal	94,{
	doevent "refill::OnRefill";
	specialeffect2 EF_HEAL2;
	percentheal 100,100;
	atcommand "@repairall";
    end;
}
turbo_room,96,115,6	script	Refill::refill	94,{
    if (getarraysize(.usable) == 0) donpcevent strnpcinfo(3)+"::OnInit";
    
    mes "[Refill Setup]";
    switch(select((refill_list$ == "") ? "Make a refill list" : 
        ":View my refill list:Edit my refill list:^FF0000Delete^000000 my refill list")) {
    case 2: // VIEW
M_view:
        explode .@items$, refill_list$, ",";
        for (set .@i,0; .@i<getarraysize(.@items$); set .@i,.@i+1) {
            explode .@part$, .@items$[.@i], ":";
            mes .@part$[1] + " " + getitemname(atoi(.@part$[0]));
            set .@total_weight, .@total_weight + getiteminfo(atoi(.@part$[0]),6)*atoi(.@part$[1]);
        }
        mes "Total Weight : ^" + ((.@total_weight <= MaxWeight) ? "008000" : "FF0000") + .@total_weight/10 + "^000000 / " + MaxWeight/10;
        break;
    case 3: // EDIT - load their refill list first
        explode .@items$, refill_list$, ",";
        for (set .@i,0; .@i<getarraysize(.@items$); set .@i,.@i+1) {
            explode .@part$, .@items$[.@i], ":";
            setd ".@"+.@part$[0], .@part$[1];
        }
    case 1: // CREATE?
        mes "What item(s) do you want?";
        select("Usable Items:Other Items:^008000All done, save my refill list^000000");
        while (@menu <= 2) {
            switch(@menu) {
            // Usable Items
            case 1:
                set .@menu$, "";
                for (set .@i,0; .@i<getarraysize(.usable); set .@i,.@i+1)
                    set .@menu$, .@menu$ + (getd(".@"+.usable[.@i]) ? getd(".@"+.usable[.@i]) : "--") +" "+ getitemname(.usable[.@i])+":";
                select(.@menu$ + "<< Back");
                while (@menu <= getarraysize(.usable)) {
                    set .@item_id, .usable[@menu-1];
                    mes "How many "+getitemname(.@item_id)+"?";
                    input .@amt,0,30000;
                    setd ".@"+.@item_id, .@amt;
                    // refresh menu
                    set .@menu$, "";
                    for (set .@i,0; .@i<getarraysize(.usable); set .@i,.@i+1)
                        set .@menu$, .@menu$ + (getd(".@"+.usable[.@i]) ? getd(".@"+.usable[.@i]) : "--") +" "+ getitemname(.usable[.@i])+":";
                    select(.@menu$ + "<< Back");
                };
                break;
            // Other Items
            case 2:
                set .@menu$, "";
                for (set .@i,0; .@i<getarraysize(.other); set .@i,.@i+1)
                    set .@menu$, .@menu$ + (getd(".@"+.other[.@i]) ? getd(".@"+.other[.@i]) : "--") +" "+ getitemname(.other[.@i])+":";
                select(.@menu$ + "<< Back");
                while (@menu <= getarraysize(.other)) {
                    set .@item_id, .other[@menu-1];
                    mes "How many "+getitemname(.@item_id)+"?";
                    input .@amt,0,30000;
                    setd ".@"+.@item_id, .@amt;
                    // refresh menu
                    set .@menu$, "";
                    for (set .@i,0; .@i<getarraysize(.other); set .@i,.@i+1)
                        set .@menu$, .@menu$ + (getd(".@"+.other[.@i]) ? getd(".@"+.other[.@i]) : "--") +" "+ getitemname(.other[.@i])+":";
                    select(.@menu$ + "<< Back");
                };
                break;
            }
            select("Usable Items:Other Items:^008000All done, save my refill list^000000");
        };
        
        // SAVE
        // concatenate into string --> item:amt,item:amt,
        for (set .@i,0; .@i<getarraysize(.usable); set .@i,.@i+1) {
            if (getd(".@"+.usable[.@i]))
                set .@refill_list$, .@refill_list$ + .usable[.@i]+":"+getd(".@"+.usable[.@i])+",";
        }
        for (set .@i,0; .@i<getarraysize(.other); set .@i,.@i+1) {
            if (getd(".@"+.other[.@i]))
                set .@refill_list$, .@refill_list$ + .other[.@i]+":"+getd(".@"+.other[.@i])+",";
        }
        if (getstrlen(.@refill_list$) > 255) {
            mes "I'm sorry, your refill list is too long. " + 
                "The limit is 25~42 items."; // 6*42=252 ~ 10*25=250
        } else if (getstrlen(.@refill_list$) > 4) {
            set refill_list$, .@refill_list$;
            mes "^008000Your refill list has been saved!^000000";
            next;
            mes "[Refill Setup]";
            goto M_view; // show them their list
        } else {
            mes "You did not select any items.";
        }
        break;
    case 4: // DELETE
        set refill_list$, "";
        mes "Your refill list has been deleted.";
        break;
    }
    close;
	OnRefill:
    // parse their refill list
    explode .@items$, refill_list$, ",";
    for (set .@i,0; .@i<getarraysize(.@items$); set .@i,.@i+1) {
        explode .@part$, .@items$[.@i], ":";
        setd ".@"+.@part$[0], (.@part$[1]);
    }
    
    for (set .@i,0; .@i<getarraysize(.usable); set .@i,.@i+1) {
        set .@amt, getd(".@"+.usable[.@i]);
        if (.@amt && countitem(.usable[.@i])<.@amt) {
            set .@get_amt, .@amt-countitem(.usable[.@i]);
            if (checkweight(.usable[.@i],.@get_amt)) {
                getitem .usable[.@i],.@get_amt;
                set .@refilled$, .@refilled$ + .@get_amt+" "+getitemname(.usable[.@i])+", ";
            } else {
                set .@overweight,1;
                break;
            }
        }
    }
    for (set .@i,0; .@i<getarraysize(.other); set .@i,.@i+1) {
        set .@amt, getd(".@"+.other[.@i]);
        if (.@amt && countitem(.other[.@i])<.@amt) {
            set .@get_amt, .@amt-countitem(.other[.@i]);
            if (checkweight(.other[.@i],.@get_amt)) {
                getitem .other[.@i],.@get_amt;
                set .@refilled$, .@refilled$ + .@get_amt+" "+getitemname(.other[.@i])+", ";
            } else {
                set .@overweight,1;
                break;
            }
        }
    }
    if (.@overweight) {
        dispbottom "You are overweight or carrying too many items.";
        emotion et_sweat,1;
    } else if (.@refilled$ != "") {
        dispbottom "Refilled " + substr(.@refilled$,0,getstrlen(.@refilled$)-3) + " !";
        emotion et_thanks,1;
    } else { // already have all items in refill list
        emotion et_best,1;
    }
    end;
	OnInit:
    setarray .usable[0],
	505,
	506,
	610,
	545,
	546,
	547,
        0; // OR just delete this line and end the previous line with a semi-colon
    setarray .other[0],
       715,
	716,
	657,
	656,
	717,
	1025,
	7136,
	7135,
	7137,
	7138,
	7139,
	602,
	1750,
1754,
1761,
1755,
1752,
1759,
1769,
1751,
1768,
	645,
	1065,
        0;
    end;
}

turbo_room,93,139,6	script	Refill and Heal#1	94,{
	doevent "refill::OnRefill";
	specialeffect2 EF_HEAL2;
	percentheal 100,100;
	atcommand "@repairall";
	save "turbo_room",84,132;
    end;
}
turbo_room,64,96,6	script	Refill and Heal#2	94,{
	doevent "refill::OnRefill";
	specialeffect2 EF_HEAL2;
	percentheal 100,100;
	atcommand "@repairall";
	save "turbo_room",71,91;
    	end;
}
//-- Refill and Healer